#Calcium Oscillation Frequency-Sensitive Gene Regulation and Homeostatic Compensation in 
#Pancreatic Beta-Cells. Bulletin of Mathematical Biology. 2017. 79-1295
#DOI: 10.1007/s11538-017-0286-1

#units: V=mV; t=ms; g=pS; I=fA
#Reference:  Bertram and Sherman. 
#ER-Calcium-based model for pancreatic islets

#Ica- calcium current
#Ik- delayed rectifier K+ current
#Ik(Ca)- Ca2+ dependent K+ current
#Ik(ATP)- nucleotide-sensitive K+ current
#Ikir- Inward rectifier K+ current (Compensating Current)
#c - cytosolic free Ca2+ concentration
#cer - ER Ca2+ concentration
#Aa- fraction of activated activator
#Ia- fraction of activated inhibitor
#TFa- fraction of active transcription factor
#mRNA- mRNA concentration
#gcmp- compensating channel conductance

#Ionic current parameters
par gca=1200, gkca=400, gk=3000
par vca=25, vk=-75, cm=5300
par taun=16, alpha=4.5e-6
par fcyt=0.01, kpmca=0.2, kd=0.3
par vn=-16, vm=-20, sn=5, sm=12
par kserca=0.4, dact=0.35, dinact=0.4
par fer=0.01, pleak=0.0005, dip3=0.5, vcytver=5
par ip3=0, gkatp=142, sa=0.1, r=0.14 taua=300000
par epser=1, freezeatp=0, astar=0.46

# ionic currents
ica(v)=gca*minf(v)*(v-vca)
ik(v)=gk*n*(v-vk)
ikca(v)=gkca*w*(v-vk)
ikatp(v)=gkatp*a*(v-vk)


#activation functions
minf(v)=1.0/(1.0+exp((vm-v)/sm))
ninf(v)=1.0/(1.0+exp((vn-v)/sn))
aainf(c)=1.0/(1.0+exp((r-c)/sa))

#fraction of K(Ca) channels activated by cytosolic Ca2+
w=c^5/(c^5+kd^5)

aux w=w

#flux of Ca2+ through the membrane
jmem=-(alpha*Ica(v)+kpmca*c)

#Ca2+ influx into the ER via SERCA 
jserca=kserca*c

#efflux out of the ER has two components
# 1. Ca2+ leak is proportional to gradient between Ca2+ and ER
jleak=pleak*(cer-c)

#net Ca2+ efflux from the ER
jer=jleak-jserca

# Leak current
p gl=170
il(v)=gl*(v-vk)

# Nucleic mRNA Production and Transcriptional Regulation
%Activator and Inhibitor Enzyme Parameters
p nA=4, KcA=0.4, pA=0.1, dA=0.004, nI=4, KcI=0.4, pIn=0.1, dI=0.004

% TF Parameters
p alphaA=0.03, betaI=0.03, KA=0.8, KI=0.1

% mRNA Parameters
p KmRNA=0.8, pM=0.001, dM=0.001

#gkir parameters
p pg=0.02, dg=0.002655, Kg=0.8

#Time Constants
p tauAc=1000, tauI=1000, tauTF=1000, tauM=1000, taug=1000

Aa'=(pA*(c^nA)/(c^nA+KcA^nA)*(1-Aa)-dA*Aa)/tauAc
Ia'=(pIn*(c^nI)/(c^nI+KcI^nI)*(1-Ia)-dI*Ia)/tauI

Ainf=(alphaA*Aa^2)/(KA^2+Aa^2)
Iinf=(betaI*Ia)/(KI+Ia)

TFa'=(Ainf*(1-TFa)-Iinf*TFa)/tauTF

mr'=(pM*TFa/(TFa+KmRNA)-dM*mr)/tauM
gcmp'=(pg*mr/(mr+Kg)-dg)/taug

#Compensating Current
params vkir=-49, skir=15
kinf(v) = 1.0/(1.0+exp((v-vkir)/skir))
icmp(v) =gcmp*kinf(v)*(v-vk)

#differential equations
v'=-(ica(v)+ik(v)+ikca(v)+ikatp(V)+il(v)+icmp(v))/cm
n'=(ninf(v)-n)/taun
c'=fcyt*(jmem+epser*jer)
cer'=-epser*fer*(vcytver)*jer
a'=(aainf(c)-a)/taua

#initial cond.
Aa(0)=0.0493
Ia(0)=0.0493
TFa(0)=0.0685
mr(0)=1.0206
gcmp(0)=0
v(0)=-38.82
n(0)=0.0064
c(0)=0.1695
cer(0)=85.3492
a(0)=0.4441

aux tsec=t/1000.0
aux tmin=t/60000

@ meth=cvode, dtmax=1, dt=20, total=18e5, maxstor=1000000, delay=2000, 
@ bounds=100000000, xp=tmin,  yp=v, toler=1.0e-7, atoler=1.0e-7
@ xlo=0, xhi=30, ylo=-80, yhi=0

done